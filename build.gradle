// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0-rc01'
        classpath 'com.novoda:bintray-release:0.9'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext {
    VERSION = version()
}

task bumpMajor {
    doLast {
        ant.propertyfile(file: 'version.properties') {
            entry(key: 'major', type: 'int', operation: '+', value: 1)
            entry(key: 'minor', type: 'int', operation: '=', value: 0)
            entry(key: 'patch', type: 'int', operation: '=', value: 0)
        }
    }
}

task bumpMinor {
    doLast {
        ant.propertyfile(file: 'version.properties') {
            entry(key: 'minor', type: 'int', operation: '+', value: 1)
            entry(key: 'patch', type: 'int', operation: '=', value: 0)
        }
    }
}

task bumpPatch {
    doLast {
        ant.propertyfile(file: 'version.properties') {
            entry(key: 'patch', type: 'int', operation: '+', value: 1)
        }
    }
}

task genReadMe {
    doLast {
        def template = file('README.md.template').text
        def result = template.replaceAll("%%version%%", version())
        file("README.md").withWriter{ it << result }
    }
}

task version {
    doLast {
        println version()
    }
}

String version() {
    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    return versionProps['major'] + "." + versionProps['minor'] + "." + versionProps['patch']
}
